skip WS      "\\s+"   ;
skip LINECOM "//.*\\n"; // dot doesn't match EOL
term PARSER  "parser" ;
term CLASS   "class"  ;
term RULE    "rule"   ;
term GOAL    "goal"   ;
term TERM    "term"   ;
term SKIP    "skip"   ;
term ID      "_*[a-zA-Z][a-zA-Z0-9]*" ;
term SEMI    ";"      ;
term ARROW   "=>"     ;
term LBRACE  "\\{"    ;
term RBRACE  "}"      ;
term PATTERN "\"[^\"]*\"" ;


rule ParserClass {
    => PARSER CLASS ID SEMI ;
}

rule Term {
    => TERM ID SEMI ;
    => TERM ID PATTERN SEMI ;
}

rule Skip {
    => SKIP ID PATTERN SEMI ;
}

/* A Possibly empty list of symbols (terms and rules) */
rule SymbolList {
    => ;
    => SymbolList ID ;
}

rule Alt {
    => ARROW SymbolList SEMI ;
}

/* An AltList cannot be empty */
rule AltList {
    => Alt ;
    => AltList Alt ;
}

rule Rule {
    => GOAL RULE ID LBRACE AltList RBRACE ;
    =>      RULE ID LBRACE AltList RBRACE ;
}

rule Statement {
    => ParserClass ;
    => Skip ;
    => Term ;
    => Rule ;
}

goal rule StatementList {
    => Statement ;
    => StatementList Statement ;
}
