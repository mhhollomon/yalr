
skip WS      r:\s+     ;
skip LINECOM r://.*\n  ; // dot doesn't match EOL
term PARSER  'parser'  ;
term CLASS   'class'   ;
term RULE    'rule'    ;
term GOAL    'goal'    ;
term TERM    'term'    ;
term SKIP    'skip'    ;
term ID      r:_*[a-zA-Z]\w* ;
term ARROW   '=>'      ;
term LBRACE  '{'       ;
term RBRACE  '}'       ;
term SEMI    ';'       ;
/* These two aren't quite correct (I don't think) */
term SQUOTE r:'(?:[^'\\]*(?:\\\\|\\'|\\)*)*' ;
term REGEX  r:r:[^\s]* ; // Note that this does not capture the closing space.


rule ParserClass {
    => PARSER CLASS ID SEMI ;
}

rule Pattern {
    => SQUOTE ;
    => REGEX ;
}

rule Term {
    => TERM ID SEMI ;
    => TERM ID Pattern SEMI ;
}

rule Skip {
    => SKIP ID Pattern SEMI ;
}

/* A Possibly empty list of symbols (terms and rules) */
rule SymbolList {
    => ;
    => SymbolList ID ;
}

rule Alt {
    => ARROW SymbolList SEMI ;
}

/* An AltList cannot be empty */
rule AltList {
    => Alt ;
    => AltList Alt ;
}

rule Rule {
    => GOAL RULE ID LBRACE AltList RBRACE ;
    =>      RULE ID LBRACE AltList RBRACE ;
}

rule Statement {
    => ParserClass ;
    => Skip ;
    => Term ;
    => Rule ;
}

goal rule StatementList {
    => Statement ;
    => StatementList Statement ;
}
