
/***************************************************
* Guarded Command Language
****************************************************/
skip WS  r:\s+ ;

term IF     'if'    ;
term FI     'fi'    ;
term DO     'do'    ; 
term OD     'od'    ;
term SKIP   'skip'  ;
term TRUE   'true'  ;
term FALSE  'false' ;

term SEMI   ';'     ;
term PIPE   '|'     ;
term ARROW  '=>'    ;
term LBR    '{'     ; 
term RBR    '}'     ;
term ASSIGN ':='    ;
term COMMA  ','     ;
term LPAR   '('     ;
term RPAR   ')'     ;
term BANG   '!'  @assoc=right   ;
// Relops need to bind tighter than the 
// logical operators
term LT     '<'   @prec=200  @assoc=left ;
term LE     '<='  @prec=LT   @assoc=left ;
term GT     '>'   @prec=LT   @assoc=left ;
term GE     '>='  @prec=LT   @assoc=left ;
term EQ     '='   @prec=LT   @assoc=left ; 
term NE     '!='  @prec=LT   @assoc=left ;
term OR     'or'  @prec=100  @assoc=left ; 
term AND    'and' @prec='or' @assoc=left ;
//
// standard math precedence
//
term STAR   '*'   @prec=200  @assoc=left ;
term SLASH  '/'   @prec='*'  @assoc=left ;
term PLUS   '+'   @prec=100  @assoc=left ;
term MINUS  '-'   @prec=PLUS @assoc=left ;

term IDENT  r:\w+  ;
term NUMBER r:[1-9][0-9]* ;


goal rule StatementList {
    => StatementList ';' Statement ;
    => Statement ;
}

rule IfStmt {
    => IF GuardedStmtList FI ;
}

rule DoStmt {
    => DO GuardedStmtList OD ;
}

rule GuardedStmtList {
    => GuardedStmt ;
    => GuardedStmtList PIPE GuardedStmt ;
}

rule GuardedStmt {
    => BoolExp ARROW Statement ;
}

rule Statement {
    => Assignment ;
    => IfStmt ;
    => DoStmt ;
    => '{' StatementList '}' ;
    => SKIP ;
}

rule Assignment {
    => IdentList ASSIGN ExpList ;
}

rule IdentList {
    => IdentList ',' IDENT ;
    => IDENT ;
}

rule ExpList {
    => ExpList ',' MathExp ;
    => MathExp ;
}

rule MathTerm {
    => NUMBER ;
    => IDENT ;
    => '(' MathExp ')' ;
}

rule MathExp {
    => MathExp '+' MathExp ;
    => MathExp '-' MathExp ;
    => MathExp '*' MathExp ;
    => MathExp '/' MathExp ;
    => MathTerm ;
}

rule BoolTerm {
    => TRUE ;
    => FALSE ;
    => '(' BoolExp ')' ;
    => '!' BoolTerm ;
}

rule RelOp {
    => LT ;
    => LE ;
    => GT ;
    => GE ;
    => EQ ;
    => NE ;
}

rule Compare {
    => MathExp RelOp MathExp ;
}

rule BoolExp {
    => BoolTerm ;
    => Compare ;
    => BoolExp OR BoolExp ;
    => BoolExp AND BoolExp ;
}
